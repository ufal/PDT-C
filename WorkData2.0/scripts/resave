#!/usr/bin/perl
use warnings;
use strict;
use experimental qw( signatures );
use feature qw{ say };

sub is_git {
    0 == system 'git rev-parse --show-toplevel >/dev/null 2>&1'
}

sub process($changed, $stayed, $file) {
    say {*STDERR} "$changed $stayed";
    say $file if $changed > $stayed;
}

my $is_git = is_git();
my @cmd   = $is_git ? qw{ git diff } : qw{ svn diff };
my $start = $is_git ? '^diff --git a/.* b/(.*)' : '^Index: (.*)';

open my $diff, '-|', @cmd or die $!;
my $changed = 0;
my $stayed  = 0;
my $skip    = 0;
my $file    = "";
while (my $line =<$diff>) {
    if ($line =~ $start) {
        my $next_file = $1;
        process($changed, $stayed, $file);
        $file = $next_file;
        say {*STDERR} $file;
        $changed = 0;
        $stayed  = 0;
        $skip    = 3;
    } elsif ($line =~ /^[-+]/) {
        ++$changed;
    } elsif (0 == index $line, ' ') {
        ++$stayed;
    }
}
process($changed, $stayed, $file);
