#!btred -e coref()
# -*- mode: cperl; encoding: utf-8; -*-
use warnings;
use strict;
use utf8;

# Finds gram, text, and compl arrows with missing targets and similar
# problems.

sub gram_must {
    my ($node) = @_;
    return 1 if $node->{t_lemma} =~ /^(?:#Q?Cor)$/;
    return 1 if $node->{t_lemma} =~ /^#(?:Rcp|PersPron)$/
        && $node->attr('a/lex.rf')
        && PML_T::GetALexNode($node)->attr('m/lemma')
            =~ m{^(?:se_\^\(zvr\._zájmeno/částice\)|svůj-1)$};
    if ($node->{t_lemma} =~ /^(?:který(?:žto)?
                             |(?:od|do)kdy
                             |ku?dy|jaký|čí|kde|odkud|kam|jak|jenž)$/x
    ) {
        my @p = $node;
        @p = map PML_T::GetEParents($_), @p
            until ! @p || grep 'v' eq ($_->attr('gram/sempos') // ""), @p;
        if (@p && grep 'RSTR' eq $_->{functor}, @p) {
            return 1
        }
    }
    return
}

my $GRAM_CAN = '^(?:#(?:PersPron|Q?Cor|Rcp)|j(?:enž|aký?)|což?|k(?:d[oy]|de|am|terý))$';

sub coref {
    my @trees;
    my %id;
    do {{
        push @trees, $this;
        while ($this = $this->following) {
            $id{ $this->{id} } = $this->root->{id};
        }
    }} while TredMacro::NextTree();

    for my $tree (@trees) {
        for my $this ($tree->descendants) {
             if ($this->{'compl.rf'}) {
                 if ('COMPL' ne $this->{functor}) {
                     print "not COMPL\t";
                     FPosition($this);
                 }
                 for my $rf (ListV($this->{'compl.rf'})) {
                     if (! exists $id{$rf}) {
                         print "no compl target\t";
                         FPosition($this);
                     } elsif ($id{$rf} ne $this->root->{id}) {
                         print "compl difftree\t";
                         FPosition($this);
                     }
                 }
             } elsif ('COMPL' eq $this->{functor}) {
                 print "missing rf\t";
                 FPosition($this);
             }

             if (my @rf = ListV($this->{'coref_gram.rf'})) {
                 if ($this->{t_lemma} !~ $GRAM_CAN && ! gram_must($this)) {
                     print "$this->{t_lemma} with gram\t";
                     FPosition($this);
                 }
                 for my $rf (@rf) {
                     if (! exists $id{$rf}) {
                         print "no gram target\t";
                         FPosition($this);
                     } elsif ($id{$rf} ne $this->root->{id}) {
                         print "$this->{t_lemma} gram difftree\t";
                         FPosition($this);
                     }
                 }
             } elsif (gram_must($this)) {
                 print "$this->{t_lemma} without gram\t";
                 FPosition($this);
             }

             if (my @rf = ListV($this->{'coref_text'})) {
                 for my $rf (map $_->{'target_node.rf'}, @rf) {
                     if (! exists $id{$rf}) {
                         print "no text target\t";
                         FPosition($this);
                     }
                 }
             }
         }
    }
}
