#!/usr/bin/perl
use warnings;
use strict;
use feature qw{ say };

use FindBin;
use lib $FindBin::Bin;

use List;

my $from = shift;
my $to   = shift;

my $list = 'List::Builder::Config'->new->build;
my %sum;
$list->for_each(sub {
    my ($self, $file) = @_;
    return if 'ALL' eq ($file->annotator // "");

    if ($file->sent) {
        my $workdir = $self->workdir($file->annotator);
        my $name = $file->name;
        my $state = (-e "$workdir/$name.cz.a"
                     && ! -e "$workdir/done/$name.cz.a")
                  ? 'todo' : 'done';
        if ('done' eq $state) {
            die "Missing done or not copied: $name" unless $file->done;

            return if $from && $file->done < $from;
            return if $to   && $file->done > $to;
        }
        $sum{ $file->annotator }{$state}{sentences} += $file->sentences;
        $sum{ $file->annotator }{$state}{forms} += $file->forms;
    } else {
        $sum{free}{$_} += $file->$_ for qw( sentences forms );
    }
});

for my $state (qw( todo done )) {
    for my $annotator (grep 'free' ne $_, sort keys %sum) {
            say join "\t", $annotator, $state,
                $sum{$annotator}{$state}{sentences} // 0,
                '(' . ($sum{$annotator}{$state}{forms} // 0) . ')';
    }
    print "\n";
}
say "FREE\t$sum{free}{sentences}\t($sum{free}{forms})";
