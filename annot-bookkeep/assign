#!/usr/bin/perl
use warnings;
use strict;
use feature qw{ say };

use FindBin;
use lib "$FindBin::Bin";
use List;
use List::Util qw{ shuffle };
use Time::Piece;

die "Usage: $0 size who" unless 2 == @ARGV;

my $size = shift;
my $who  = shift;

my $list = list();
open my $in, '<', $list or die "Can't open $list: $!";
chomp( my @lines = <$in> );

@lines = shuffle(@lines);
my $date = localtime->strftime('%y%m%d');
my $sum = 0;
for my $line (@lines) {
    next if $line =~ /^#/;

    my @cols = split /\t/, $line;
    next if $cols[SENT];

    if ($sum + $cols[SENTENCES] < $size) {
        $sum += $cols[SENTENCES];
        $cols[ANNOTATOR] = $who;
        $cols[SENT] = $date;
    }
    $line = join "\t", @cols;
}
for my $line (sort { $a cmp $b } @lines) {
    say $line;
}
say STDERR "$sum/$size";
