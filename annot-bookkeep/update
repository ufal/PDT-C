#!/usr/bin/perl
use warnings;
use strict;

use feature qw{ say };

use Path::Tiny ();

use FindBin;
use lib $FindBin::Bin;

use List;

my $new;
sub update {
    my ($self, $file) = @_;
    if ($file->sent && $file->annotator ne 'ALL') {
        my $workdir = $self->workdir($file->annotator);
        my $name = $file->name;
        my $done_file = "$workdir/done/$name.cz.a";
        if (-e $done_file) {
            open my $svn, '-|', qw( svn log -q ), $done_file
                or die "Can't run svn: $!";
            my $date;
            while (<$svn>) {
                if (my ($tmp_date)
                    = /^r[0-9]+\ \|\ [^|]+\ \|\ 
                       [0-9]{2}([0-9]{2}-[0-9]{2}-[0-9]{2})
                       \ /x
                ) {
                    $date = $tmp_date =~ s/-//gr;
                }
            }
            if ($date) {
                if ($file->done && $date ne $file->done) {
                    die "Wrong date at $name.\n";
                }
                $file->done($date);
            }
        }
    }
    say {$new} $file->serialise;
}

my $list = 'List::Builder::Config'->new->build;

my $tmp = 'Path::Tiny'->tempfile;
$new = $tmp->filehandle('>');
say {$new} $list->header;
$list->for_each(\&update);
close $new;

$tmp->copy($list->file) or die "Can't rename to ", $list->file;
warn $tmp;
